/*
 * Copyright 2015-2020 Ritense BV, the Netherlands.
 *
 * Licensed under EUPL, Version 1.2 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://joinup.ec.europa.eu/collection/eupl/eupl-text-eupl-12
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" basis,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def signingConfigSet = false
if(System.getenv('SIGNING_KEY_PATH') &&
    System.getenv('SIGNING_KEY_PASSWORD')
) {
    signingConfigSet = true
}

if(signingConfigSet) {
    apply plugin: 'signing'
}

if(System.getenv('SONATYPE_USR') &&
        System.getenv('SONATYPE_PSW')
) {
    apply plugin: 'maven-publish'
}

publishing {
    repositories {
        maven {
            name "Sonatype"
            afterEvaluate {
                credentials {
                    username System.getenv('SONATYPE_USR') ? System.getenv('SONATYPE_USR') : '<unset>'
                    password System.getenv('SONATYPE_PSW') ? System.getenv('SONATYPE_PSW') : '<unset>'
                }
            }
            def stagingRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = project.ext.get("pluginProperties").getOrDefault('pluginVersion', '').endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'com.ritense.valtimoplugins'

            pom {
                url = 'https://exchange.gzac.nl'
                licenses {
                    license {
                        name = 'Licensed under EUPL, Version 1.2 (the "License");'
                        url = 'https://joinup.ec.europa.eu/collection/eupl/eupl-text-eupl-12'
                    }
                }
                scm {
                    connection = "git@github.com:generiekzaakafhandelcomponent/Plugins.git"
                    developerConnection = "git@github.com:generiekzaakafhandelcomponent/Plugins.git"
                    url = "https://github.com/generiekzaakafhandelcomponent/Plugins"
                }
            }

            if (components.hasProperty("javaPlatform")) {
                from components.javaPlatform
            } else {
                from components.java
            }
        }
    }
}

if(signingConfigSet) {
    signing {
        def signingKeyBase64 = System.getenv("SIGNING_KEY")
        def signingKeyBytes= signingKeyBase64.decodeBase64()
        def signingKey = signingKeyBytes.toString(Charsets.UTF_8)
        def signingKeyPassword = System.getenv('SIGNING_KEY_PASSWORD')

        useInMemoryPgpKeys(signingKey, signingKeyPassword)
        sign configurations.archives
        sign publishing.publications.maven
    }
}

def filterVersionLine(bufferedReader) {
    def line
    def versionLine
    while ((line = bufferedReader.readLine()) != null) {
        if((line.startsWith('version') && !line.startsWith('version.')) || line.startsWith('projectVersion') || line.startsWith('version.semver')) {
            versionLine = line.split('=')
        }
    }
    return versionLine
}

def parseProjectVersion(filePath) {
    def fileString = new File(filePath).getText('UTF-8')
    def bufferedReader = new BufferedReader(new StringReader(fileString))
    def versionLine = filterVersionLine(bufferedReader)

    // strip off -SNAPSHOT postfixes
    def projectVersion = versionLine[1]
    projectVersion = projectVersion.trim()
    if(projectVersion.contains('-')) {
        projectVersion = projectVersion.split('-')[0]
    }
    return projectVersion
}
